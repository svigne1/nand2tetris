
/** It is used both as a body part of the snake as well as it's food */
class SnakeCell {
    field int x, y;
    field boolean state; 
    field SnakeCell next;
    field int size, spacing;             

    /* Creates a SnakeCell. */
    constructor SnakeCell new(int _x, int _y, SnakeCell _next, int _size, int _spacing) {
        let x = _x;
        let y = _y;
        let next = _next;
        let size = _size;
        let spacing = _spacing;
        let state = false;
        return this;
    }

    /** Draws the snake on the screen. */
    method void draw() {
        // White Background
        // do Screen.setColor(false);
        // do Screen.drawRectangle(x, y, x + size + 1, y + size + 1);
        // Black Cell
        do Screen.setColor(state);
        do Screen.drawRectangle(x, y, x + size, y + size);

        return;
    }
    
    /** Reverses the state from hidden to visible & vice-versa */
    method void toggle() {
        let state = ~state;
        do draw();
        return;
    }

    method void moveNextPosition(int direction){
        // Up
        if (direction = 1) {
            let y = y - size - spacing;
        }
        // Down
        if (direction = 2) {
            let y = y + size + spacing;
        }
        // Left
        if (direction = 3) {
            let x = x - size - spacing;
        }
        // Right
        if (direction = 4) {
            let x = x + size + spacing;
        }
        return;
    }
    
    /** Disposes the whole snake starting from tail */
    method void dispose() {
        if (~(next = null)) {
            do next.dispose();
        }
        // Uses an OS routine to recycle this object.
        do Memory.deAlloc(this);
        return;
    }

    /** Get & Set */
    method int getX() { return x; }
    method int getY() { return y; }
    method SnakeCell getNext() { return next; }
    method boolean getState() { return state; }
    method void setX(int v) { 
        let x=v;
        return;
    }
    method void setY(int v) { 
        let y=v;
        return;
    }
    method void setNext(SnakeCell v) { 
        let next = v;
        return;  
    }

}

// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    /* 
      -> A-register gets set in this following truth table way
        - where 15th bit, is instruction[15], which is an A-instruction if 0 or C-instruction if 1.
        - If C-instruction, then A is modified using ALU's output only when d1 bit is ON, which is instruction[5]

      15th bit | 5th bit | set | value

        1           1       1       comp
        1           0       0       anything, say instruction
        0           0       1       instruction
        0           1       1       instruction

      -> Re-arrange the table, you get
      15th bit | 5th bit | set | value 
        
        0           0       1       instruction
        0           1       1       instruction
        1           0       0       anything, say instruction
        1           1       1       comp
        
        So, instruction = NAND(15th, 5th bit)

      -> Make 5th bit as NOT(5th bit), you get
      15th bit | !5th bit | set | value
        
        0           0       1       instruction
        0           1       1       instruction
        1           0       1       comp
        1           1       0       anything, say instruction
        
        So, set = NAND(15th, !5th)

        Using these, setting A-register's T+1 output (Atplus) to computation or instruction or null (the above truth table helps us to decide, which of these should happen) where computation is computed from an ALU that uses A-register's output from Time T (At)
    */
    
    Nand(a=instruction[15], b=instruction[5], out=setInstruction);
    Not(in=instruction[5], out=not5thBit);
    Nand(a=instruction[15], b=not5thBit, out=setA);

    Mux16(a=computation, b=instruction, sel=setInstruction, out=Avalue);
    ARegister(in=Avalue, load=setA, out=Atplus, out=At);


    /* 
        For D-register,
        instruction[15] (C-instruction) == 1 & instruction[4] == 1 (D bit in C-instruction)
    */
    And(a=instruction[15], b=instruction[4], out=setD);
    DRegister(in=computation, load=setD, out=Dtplus, out=Dt);

    /* 
        For ALU,
        x is D,
        y is A if instruction[12] = 0 
            and M if instruction[12] = 1,
        Control bits are instruction[11..6],
        out = computation
    */
    Mux16(a=At, b=inM, sel=instruction[12], out=AorMasY);
    ALU(x=Dt, y=AorMasY, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=computation, zr=zr, ng=ng);

    /* 
        For M, 
        writeM = instruction[15] = 1 (C-instruction) & instruction[3] = 1 (Set to M bit in C-instruction)
        outM = computation
        addressM = At (The value of A, before modified)
    */
    And(a=instruction[3], b=instruction[15], out=writeM);
    Or16(a=computation, b=false, out=outM);
    Or16(a=At, b=false, out[0..14]=addressM);
    
    /* 
        For PC,
        Say instruction = ins
        ins[0..2]
            (if ins[0] = greater = 1 & pos == 1 & !zr(not_zero) == 1, then A,
            or if ins[1] = equal = 1 & zr == 1, then A
            or if ins[2] = lesser = 1 & ng == 1, then A
            or if ins[0] & ins[1] & ins[2] == unconditional = 1, A)
            AND ins[15] == 1 (if it's a C-instruction)
        If !A, then 
            jump to PC+1,
        If Reset, reset.
        Code needs to be read in reverse
    */
    
    Not(in=ng, out=pos);
    Not(in=zr, out=notZero);
    And(a=instruction[0], b=pos, out=greater1); // if >
    And(a=greater1, b=notZero, out=greater);
    And(a=instruction[1], b=zr, out=equal);
    And(a=instruction[2], b=ng, out=lesser);
    And(a=instruction[0], b=instruction[1], out=unconditional1);
    And(a=instruction[2], b=unconditional1, out=unconditional);
    Or(a=greater, b=equal, out=bigOR1);
    Or(a=bigOR1, b=lesser, out=bigOR2);
    Or(a=bigOR2, b=unconditional, out=bigOR3);
    And(a=instruction[15], b=bigOR3, out=jump2A);
    Not(in=jump2A, out=inc);

    PC(in=At, load=jump2A, inc=inc, reset=reset, out[0..14]=pc);

}